#line 268 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
struct Buffer
{
  sp: StringPiece;
  pos: int;

  fun atend => self.pos >= self.sp.len.int;

  fun get =>
    if self.atend then char ""
    else (self.sp.data) . (self.pos)
  ;

  proc next {
    if not self*.atend do
      pre_incr self.pos;
    done
  }

  fun advanced =>
    if self.atend then self
    else Buffer (self.sp, self.pos + 1)
  ;
}

ctor Buffer (p:&string) =>
  Buffer (p.StringPiece,0)
;

instance Str[Buffer] {
  fun str (b:Buffer) => "@"+b.pos.str;
}

// hack, ignore underlying data.. FIXME
instance Eq[Buffer] {
  fun == (a:Buffer, b:Buffer) => a.pos == b.pos;
}


#line 309 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
chip match_string (s:string)
  connector io
    pin inp: %<Buffer
    pin out: %>Buffer
{
nextmatch:>
  var b = read io.inp;
  for i in 0..< s.len.int do
    if s.[i] != b.get goto nextmatch;
    b&.next;
  done
  write (io.out, b);
  goto nextmatch;
}

#line 326 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
chip match_white
  connector io
    pin inp: %<Buffer
    pin out: %>Buffer
{
  while true do
    var b = read io.inp;
    while not b.atend and b.get <= char ' ' perform b&.next;
    write (io.out,b);
  done
}

#line 341 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
chip match_regex (r:RE2)
  connector io
    pin inp: %<Buffer
    pin out: %>Buffer
{
  while true do
    var b = read io.inp;
//println$ "Match regex " + r.str;
    var matched = varray[StringPiece] (1uz,StringPiece());
    var result = Match(r,b.sp,b.pos,ANCHOR_START,matched.stl_begin,1);
//println$ "Match result " + result.str;
    if result do
//println$ "Matched OK, match len = " + matched.0.len.str;
      var b2 = Buffer (b.sp,b.pos+matched.0.len.int);
//println$ "Writing buffer = " + b2.str;
      write(io.out,b2);
    done
  done
}

#line 365 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
device cident_matcher = match_regex (RE2 "[A-Za-z][A-Za-z0-9_]*");

#line 371 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
device decimal_integer_matcher = match_regex (RE2 "[0-9]+");

#line 377 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
chip match_string_literal
  connector io
    pin inp: %<Buffer
    pin out: %>Buffer
{
restart:>
  var b = read io.inp;
  if b.atend goto restart; // end of data
  var leadin = b.get;
//println$ "string literal matcher got char " + leadin.str;
  if not (leadin in (char '"', char "'")) goto restart;
//println$ "Got valid string start .. ";
  b&.next;
  if b.atend goto restart;
  var ch = b.get;
  while ch != leadin do
    b&.next;
    if b.atend goto restart;
    ch = b.get;
    if ch == char "\n" goto restart; // end of line
  done
  b&.next;
  io.out `write` b;
  goto restart;
}

#line 425 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
typedef pardat_t[T] = Buffer * T;

chip leaf_capture[T]
  (
    scan: iochip_t[Buffer,Buffer],
    newstate: pardat_t[T] * Buffer -> pardat_t[T]
  )
  connector io
    pin inp: %<pardat_t[T]
    pin out: %>pardat_t[T]
{
while true do
  var x = read io.inp;
  noinline proc handler (var x: pardat_t[T]) () {
    var b,pd = x;
    var rin,win= mk_ioschannel_pair[Buffer]();
    var rout,wout= mk_ioschannel_pair[Buffer]();
    spawn_fthread (scan (inp=rin,out=wout));
    write(win,b);
    var b2 = read rout; // this can block forever if scan fails
    var s = newstate ((b,pd),b2);
    write (io.out,s);
  }
  var y = handler x;
  spawn_fthread y;
done
}



#line 460 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"
chip optional (p:iochip_t[Buffer,Buffer])
  connector io
    pin inp: %<Buffer
    pin out: %>Buffer
{
  device both = tryall_list ([
    p,
    debug_buffer[Buffer] "Epsilon"
  ]);
  circuit
    wire io.inp to both.inp
    wire io.out to both.out
  endcircuit
}

#line 477 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"

chip oneormore_matcher (A:iochip_t[Buffer,Buffer])
connector chans
  pin inp: %<Buffer
  pin out: %>Buffer
{
 device As = oneormore_matcher A;
 device As2 = pipeline_list (A,As).list;
 device Ass = tryall_list (A, As2).list;
 circuit
   wire chans.inp to Ass.inp
   wire chans.out to Ass.out
 endcircuit
}

#line 494 "/home/travis/build/felix-lang/felix/src/packages/chips.fdoc"

union symbol_t =
  | Terminal of string
  | Nonterminal of string
;

typedef production_t = list[symbol_t];
typedef alternatives_t = list[production_t];

typedef grammar_t = (start:string, strdict[alternatives_t]);

chip parse_simple_grammar
  connector io
    pin inp: %<string
    pin out: %>grammar_t
{
}


